/*
 * SoundBox - Android Music Player
 * Copyright (C) 2013 Iv√°n Arcuschin Moreno
 *
 * This file is part of SoundBox.
 *
 * SoundBox is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 2 of the License, or
 * (at your option) any later version.
 *
 * SoundBox is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with SoundBox.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 19
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aild.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    if (project.hasProperty("SoundBox.properties")
            && new File(project.property("SoundBox.properties")).exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file(project.property("SoundBox.properties"))))

        signingConfigs {
            release {
                storeFile file(props['keystore'])
                storePassword props['keystore.storePassword']
                keyAlias props['keystore.alias']
                keyPassword props['keystore.keyPassword']
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
            android.applicationVariants.all { variant ->
                if (variant.buildType.name == "release") {
                    variant.outputFile = new File(props['outputDir'], "soundbox-release.apk")
                }
            }
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:19.+'
    compile 'com.android.support:support-v4:19.+'
    compile 'com.nineoldandroids:library:2.4.0'
}